import os
import pandas as pd
from abag_affinity.utils.config import read_yaml, get_data_paths

config = read_yaml("../../abag_affinity/config.yaml")
skempi_df_path, pdb_path = get_data_paths(config, "SKEMPI.v2")
skempi_df = pd.read_csv(skempi_df_path, sep=";")

data_path = config["DATA"]["path"]
skempi_folder_path = os.path.join(data_path, config["DATA"]["SKEMPI.v2"]["folder_path"])
mutated_output_folder = os.path.join(skempi_folder_path, config["DATA"]["SKEMPI.v2"]["mutated_pdb_path"])

wiltype_pdbs = [ pdb_id.split("_")[0] for pdb_id in skempi_df["#Pdb"].tolist() ]
mutation_codes = skempi_df["Mutation(s)_cleaned"].tolist()


rule final_output:
    input:
        expand(skempi_folder_path + '/mutated_relaxed/{pdb_id}/{mutation}.pdb', zip, pdb_id=wiltype_pdbs, mutation=mutation_codes)

rule relax_wildtype:
    input:
        pdb_path + '/{pdb_id}.pdb'
    output:
        skempi_folder_path + "/relaxed_wildtype/{pdb_id}.pdb",
    conda: "../../envs/rosetta_environment.yml"
    script: "scripts/relax.py"

rule mutate_wildtype:
    input:
        skempi_folder_path + "/relaxed_wildtype/{pdb_id}.pdb",
    output:
        skempi_folder_path + "/mutated/{pdb_id}/{mutation}.pdb"
    conda: "../../envs/rosetta_environment.yml"
    script: "scripts/mutate.py"

rule relax_mutated:
    input:
        skempi_folder_path +"/mutated/{pdb_id}/{mutation}.pdb"
    output:
        skempi_folder_path +"/mutated_relaxed/{pdb_id}/{mutation}.pdb"
    conda: "../../envs/rosetta_environment.yml"
    script: "scripts/relax.py"

