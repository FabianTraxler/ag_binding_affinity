import pandas as pd

input_filepath = "../../../resources/dms_phillips21/pdbs"
info_df = pd.read_csv("../../../data/dms_phillips21/dms.csv")

all_complexes = info_df.apply(lambda row: row["antibody_id"] + "_" + row["pdb_file"], axis=1)
unique_complexes = all_complexes.unique().tolist()
all_complexes = all_complexes.tolist()

all_mutations = info_df.apply(lambda row: row["mutation_code"], axis=1).tolist()


out_folder = "../../../data/dms_phillips21"

rule final_output:
    input:
        out_folder + "/generation_summary.csv"

# use not relaxed wildtye for mutation
rule score_wildtype:
    input:
        input_filepath + "/{complex}.pdb"
    output:
        out_folder + "/wildtype/{complex}.pdb",
    conda: "../../../envs/rosetta_environment.yml"
    script: "scripts/score.py"

rule mutate_wildtype:
    input:
        input_filepath + "/{complex}.pdb"
    output:
        out_folder + "/mutated_wildtype/{complex}_{mutation}.pdb"
    conda: "../../../envs/rosetta_environment.yml"
    resources:
        mem_mb=8000
    script: "scripts/mutate.py"

rule relax_mutated:
    input:
        out_folder +"/mutated_wildtype/{complex}_{mutation}.pdb"
    output:
        out_folder + "/mutated_relaxed/{complex}_{mutation}.pdb"
    conda: "../../../envs/rosetta_environment.yml"
    resources:
        mem_mb=4000
    script: "scripts/relax.py"


rule calculate_change_in_binding_affinity:
    input:
        expand(out_folder + '/wildtype/{complex}.pdb', complex=unique_complexes),
        expand(out_folder + "/mutated_wildtype/{complex}_{mutation}.pdb", zip, complex=all_complexes, mutation=all_mutations),
        expand(out_folder + "/mutated_relaxed/{complex}_{mutation}.pdb", zip, complex=all_complexes, mutation=all_mutations),
    output:
        out_folder + "/generation_summary.csv"
    params:
        mutation_codes=all_mutations,
        complexes=all_complexes,
        out_folder=out_folder
    conda: "../../../envs/rosetta_environment.yml"
    script: "scripts/calculate_affinity_changes.py"
